5 CALL MAGNIFY(3)
7 CALL SCREEN(15)
10 CALL CLEAR
15 PRINT "(J)OYSTICK OR (K)EYBOARD?"
16 ACCEPT AT(23,27)SIZE(1)VALIDATE("JK"):C$ :: IF C$="" THEN 16
20 CALL COLOR(1,7,1,2,13,1)
25 OPTION BASE 1
30 DIM RC(99)
40 DIM IT(18)
50 CALL DELSPRITE(ALL)
55 CALL CLEAR
60 DISPLAY AT(8,10):"NEW GAME"
70 DISPLAY AT(10,10):"OLD GAME"
80 DISPLAY AT(12,10):"QUIT"
90 C=1
100 CALL HCHAR(C*2+6,7,44)
110 CALL CM(C$,X,Y)
115 CALL HCHAR(C*2+6,7,32)
120 C=C-SGN(Y):: IF C=4 THEN C=1 ELSE IF C=0 THEN C=3
130 CALL KEY(1,K,S):: IF K<>18 THEN 100
140 CALL CLEAR
150 IF C<>3 THEN 160
151 DISPLAY AT(1,1):"ARE YOU SURE? (Y/N)" :: ACCEPT AT(1,27)SIZE(1)VALIDATE("YN"):A$
152 IF A$="" THEN 151
153 IF A$="Y" THEN END ELSE 55
160 IF C=1 THEN 170
161 OPEN #1:"DSK1.CQSAVE",INPUT ,FIXED
162 INPUT #1:X$
163 CLOSE #1
164 IT(1)=VAL(SEG$(X$,1,3))
165 FOR A=2 TO 18 :: IT(A)=VAL(SEG$(X$,A+2,1)):: NEXT A
166 GOTO 180
170 FOR A=1 TO 18 :: IT(A)=0 :: NEXT A
171 IT(7)=3 :: IT(8)=3
180 RANDOMIZE 159
190 FOR A=1 TO 11
200 R=INT(RND*99)+1
210 IF RC(R)<>0 THEN 200
220 RC(R)=A :: NEXT A
225 Y=70 :: X=120
227 CALL DELSPRITE(ALL)
230 CALL ROOMDRAW(R)
240 IF RC(R)<>11 THEN 260
250 CALL SPRITE(#1,128,2,48,80,#2,128,2,48,160,#3,128,2,160,80,#4,128,2,160,160)
255 GOTO 280
260 IF RC(R)=0 THEN 280
261 IF RC(R)<>10 THEN 270
262 CALL SPRITE(#1,112,5,96,128)
263 GOTO 280
270 IF IT(RC(R)+9)=1 THEN 280
271 DISPLAY AT(11,13):"„†":TAB(13);"†„"
275 CALL SPRITE(#1,65,1,81,97)
280 CALL SPRITE(#28,100,5,Y,X)
285 IF RND<.15 AND RC(R)=0 THEN CALL SET(IT())
290 CALL CM(C$,X,Y)
291 CALL KEY(1,K,S):: IF K=18 THEN GOSUB 5000
300 CALL MOTION(#28,-Y*2,X*2)
310 CALL COINC(ALL,A)
320 IF A THEN 390
330 CALL POSITION(#28,Y,X)
340 IF Y<15 THEN CALL GCHAR(1,9,A):: IF A=37 THEN CALL LOCATE(#28,15,X)ELSE Y=155 :: R=R-9 :: GOTO 227
350 IF Y>160 THEN CALL GCHAR(24,9,A):: IF A=37 THEN CALL LOCATE(#28,155,X)ELSE Y=16 :: R=R+9 :: GOTO 227
360 IF X<15 THEN CALL GCHAR(9,1,A):: IF A=37 THEN CALL LOCATE(#28,Y,15)ELSE X=220 :: R=R-1 :: GOTO 227
370 IF X>225 THEN CALL GCHAR(9,32,A):: IF A=37 THEN CALL LOCATE(#28,Y,225)ELSE X=15 :: R=R+1 :: GOTO 227
380 GOTO 290
390 CALL MOTION(#28,0,0)
395 IF RC(R)<>10 THEN 500
400 FOR A=IT(8)TO IT(7)
410 CALL SOUND(-100,-3,0)
420 NEXT A
430 IT(8)=IT(7)
440 CALL DELSPRITE(#1)
450 GOTO 290
500 IF RC(R)<>11 THEN 550
510 C=0 :: FOR A=1 TO 4 :: CALL COINC(#A,#28,16,Q):: IF Q THEN C=A
520 NEXT A
530 ON C GOSUB 1000,2000,3000,4000
540 GOTO 225
550 IF RC(R)<>10 THEN 580
560 FOR A=IT(8)TO IT(7):: CALL SOUND(10,110*A,0):: NEXT A
570 IT(8)=IT(7):: GOTO 225
580 IF RC(R)<>0 THEN 650
590 IT(IT(2))=IT(IT(2))+IT(3)
600 CALL SOUND(100,500,0)
610 IF IT(2)=4 THEN IT(4)=1
620 IF IT(2)<3 AND IT(1)>999 THEN IT(1)=999
630 IF IT(2)=5 AND IT(5)>9 THEN IT(5)=9
632 IF IT(2)=6 AND IT(6)>9 THEN IT(6)=9
634 IF IT(2)=7 AND IT(7)>9 THEN IT(7)=9
640 IF IT(2)=8 AND IT(8)>IT(7)THEN IT(8)=IT(7)
645 CALL DELSPRITE(#1)
647 GOTO 225
650 CALL CLEAR :: CALL DELSPRITE(ALL)
655 A=1
660 ON RC(R)GOSUB 6000,6200,6400,6600,6800,7000,7200,7400,7600
665 IF A THEN 225
667 CALL CLEAR :: CALL DELSPRITE(ALL)
668 FOR A=1100 TO 110 STEP -200 :: CALL SOUND(-10,A,0):: NEXT A
670 DISPLAY AT(12,9):"GAME OVER"
680 DISPLAY AT(18,3):"SAVE GAME"
690 DISPLAY AT(19,3):"CONTINUE"
691 C=1
693 CALL HCHAR(17+C,3,44)
700 CALL CM(C$,X,Y)
710 CALL VCHAR(18,3,32,2)
720 C=C-SGN(Y):: IF C=0 THEN C=2 ELSE IF C=3 THEN C=1
730 CALL KEY(1,K,S):: IF K<>18 THEN 693
740 IF C=2 THEN 800
750 OPEN #1:"DSK1.CQSAVE",OUTPUT,FIXED
760 X$=SEG$(STR$(IT(1))&".00",1,3)
770 FOR A=2 TO 18 :: X$=X$&STR$(IT(A)):: NEXT A
780 PRINT #1:X$
790 CLOSE #1
800 GOTO 50
1000 CALL CLEAR :: CALL DELSPRITE(ALL)
1010 DISPLAY AT(1,11):"STORE"
1015 DISPLAY AT(1,20):IT(1);"X $"
1020 DISPLAY AT(3,3):"MAGIC SWORD      100"
1030 DISPLAY AT(4,3):"LIFE POTION       25"
1040 DISPLAY AT(5,3):"MAGIC POTION      40"
1050 DISPLAY AT(6,3):"ARMOUR            50"
1060 DISPLAY AT(7,3):"MAGIC ARMOUR      95"
1080 DISPLAY AT(9,3):"EXIT"
1090 C=1
1100 CALL HCHAR(C+2,3,44)
1110 CALL CM(C$,X,Y)
1120 CALL HCHAR(C+2,3,32)
1130 C=C-SGN(Y):: IF C=0 THEN C=7 ELSE IF C=8 THEN C=1
1140 CALL KEY(1,K,S)
1150 IF K<>18 THEN 1100
1151 IF C=6 THEN 1100
1155 IF C=7 THEN RETURN
1160 ON C GOTO 1170,1200,1230,1260,1320,1380
1170 IF IT(1)<100 THEN 1100
1175 CALL SOUND(100,550,0)
1180 IT(1)=IT(1)-500
1185 IT(4)=1
1190 RETURN
1200 IF IT(1)<25 THEN 1100
1205 CALL SOUND(100,110,0)
1210 IT(1)=IT(1)-25 :: IT(6)=IT(6)+1 :: IF IT(6)>9 THEN IT(6)=9
1220 RETURN
1230 IF IT(1)<40 THEN 1100
1235 CALL SOUND(100,999,0)
1240 IT(1)=IT(1)-40 :: IT(6)=IT(6)+2 :: IF IT(6)>9 THEN IT(6)=9
1250 RETURN
1260 IF IT(1)<50 THEN 1100
1265 CALL SOUND(100,675,0)
1300 IT(1)=IT(1)-50 :: IT(5)=IT(5)+1
1305 IF IT(5)>9 THEN IT(5)=9
1310 RETURN
1320 IF IT(1)<95 THEN 1100
1325 CALL SOUND(100,223,0)
1330 IT(1)=IT(1)-95 :: IT(5)=IT(5)+2
1335 IF IT(5)>9 THEN IT(5)=9
1340 RETURN
2000 CALL CLEAR :: CALL DELSPRITE(ALL)
2010 DISPLAY AT(1,9):"PAWN SHOP"
2020 DISPLAY AT(4,3):"SELL LIFE"
2030 DISPLAY AT(5,3):"EXIT"
2040 C=1
2050 CALL HCHAR(C+3,3,44)
2060 CALL CM(C$,X,Y)
2070 CALL HCHAR(C+3,3,32)
2080 C=C-SGN(Y):: IF C=0 THEN C=2 ELSE IF C=3 THEN C=1
2090 CALL KEY(1,K,S):: IF K<>18 THEN 2050
2100 IF C=2 THEN RETURN
2110 DISPLAY AT(10,1):"HOW MUCH? 0"
2120 C=0
2130 CALL CM(C$,X,Y)
2140 C=C-SGN(Y):: IF C<0 THEN C=INT(IT(8))ELSE IF C>INT(IT(8))THEN C=0
2150 DISPLAY AT(10,11):C
2160 CALL KEY(1,K,S):: IF K<>18 THEN 2130
2170 DISPLAY AT(11,1):"YOU GET             :0"
2180 FOR A=1 TO C :: CALL SOUND(10,-6,0):: IT(1)=IT(1)+10 :: DISPLAY AT(11,22):A*10 :: NEXT A
2185 IT(8)=IT(8)-C
2187 IF IT(8)=0 THEN IT(8)=.5
2190 CALL KEY(1,K,S):: IF K<>18 THEN 2190
2200 RETURN
3000 CALL CLEAR :: CALL DELSPRITE(ALL)
3010 DISPLAY AT(1,7):"FORTUNE TELLER"
3020 DISPLAY AT(3,3):"A CURSE HAS BEEN PLACED"
3030 DISPLAY AT(4,1):"ON US  COLLECT THE 8"
3040 DISPLAY AT(5,1):"CRYSTALS AND POWER SOURCE"
3050 DISPLAY AT(6,1):"TO SAVE US"
3060 CALL KEY(1,K,S):: IF K<>18 THEN 3060
3070 RETURN
4000 CALL CLEAR :: CALL DELSPRITE(ALL)
4010 DISPLAY AT(1,8):"CRYSTAL ROOM"
4040 DISPLAY AT(3,3):"CAN NOT ENTER WITHOUT ALL   CRYSTALS"
4050 CALL KEY(1,K,S):: IF K<>18 THEN 4050 ELSE RETURN
5000 CALL CLEAR
5005 CALL DELSPRITE(ALL)
5010 DISPLAY AT(2,5):"$ X";IT(1)
5040 CALL SPRITE(#1,136,-5*(IT(4)=1)-2*(IT(4)=0),64,40)
5050 DISPLAY AT(12,5):"ARMOUR:";IT(5)
5060 DISPLAY AT(14,5):"POTIONS:";IT(6)
5065 DISPLAY AT(16,5):"LIFE:"
5070 CALL HCHAR(16,12,135,IT(7))
5080 CALL HCHAR(16,12,44,INT(IT(8)))
5090 IF IT(8)<>INT(IT(8))THEN CALL HCHAR(16,INT(IT(8))+12,33)
5120 FOR A=10 TO 17
5130 IF IT(A)THEN CALL SPRITE(#A-6,120,2,160+16*(A>13),(A+5*(A>13))*12)
5140 NEXT A
5145 C=1
5150 DISPLAY AT(24,1):" POTION  QUIT  SAVE"
5170 CALL HCHAR(24,-3*(C=1)-11*(C=2)-17*(C=3),44)
5180 CALL CM(C$,X,Y)
5190 C=C+SGN(X)
5200 IF C=4 THEN C=1 ELSE IF C=0 THEN C=3
5210 CALL KEY(1,K,S)
5220 IF K<>18 THEN 5150
5270 IF C<>1 THEN 5290
5275 IF IT(6)=0 THEN 5150
5280 IT(6)=IT(6)-1
5282 FOR A=IT(8)TO IT(7):: CALL SOUND(100,330+A,0):: NEXT A
5284 IT(8)=IT(7)
5286 GOTO 5000
5290 IF C=3 THEN 750
5295 CALL CLEAR :: CALL DELSPRITE(ALL)
5300 GOTO 225
6000 DISPLAY AT(1,1):"THE ROOM IS EMPTY AND THE   CRYSTAL IS YOURS"
6010 IT(10)=1
6020 CALL KEY(1,K,S):: IF K<>18 THEN GOTO 6020 ELSE RETURN
6200 DISPLAY AT(1,1):"SWORDS OF SEPARATION"
6210 FOR A=1 TO 8 :: CALL SPRITE(#A,108,7,A*20,1,0,A*5):: NEXT A
6220 CALL SPRITE(#9,120,5,1,128,0,5)
6230 CALL SPRITE(#10,100,5,170,128)
6240 CALL CM(C$,X,Y)
6241 CALL POSITION(#10,@,A)
6245 IF Y=1 AND @>169 THEN CALL MOTION(#10,0,0):: GOTO 6260
6250 CALL MOTION(#10,-Y*5,X*5)
6260 CALL COINC(ALL,A):: IF A=0 THEN 6240
6270 CALL COINC(#9,#10,20,A)
6275 IF A THEN 6300
6280 IT(8)=IT(8)-.5 :: IF IT(8)=0 THEN RETURN
6290 CALL SOUND(100,-1,0):: GOTO 6240
6300 CALL SOUND(100,550,0):: IT(11)=1 :: RETURN
6400 DISPLAY AT(1,2):"CATCH THE BOUNCING CRYSTAL"
6410 CALL SPRITE(#1,120,5,100,100,-25,25)
6420 CALL SPRITE(#2,100,5,100,100):: @=0
6430 CALL CM(C$,X,Y)
6440 CALL MOTION(#2,-Y*6,X*6)
6450 CALL COINC(ALL,A):: IF A THEN 6500
6460 @=@+1 :: IF @>30 THEN CALL SOUND(100,-6,0):: CALL SCREEN(2):: CALL SCREEN(15):: IT(8)=IT(8)-.5 :: IF IT(8)=0 THEN RETURN
6470 IF @>30 THEN @=0
6480 IF RND>.6 THEN CALL MOTION(#1,25*(RND*3-1),25*(RND*3-1))
6490 GOTO 6430
6500 CALL SOUND(100,550,0):: IT(12)=1 :: RETURN
6600 DISPLAY AT(1,1):"THIS CRYSTAL COSTS 1 MONEY"
6610 IF IT(1)<1 THEN DISPLAY AT(2,10):"TOO BAD" :: GOTO 6700
6620 DISPLAY AT(2,1):"YOU GET IT!" :: IT(1)=IT(1)-1
6630 IT(13)=1
6700 CALL KEY(1,K,S):: IF K<>18 THEN 6700
6710 RETURN
6800 DISPLAY AT(1,1):"THE ENTRACE IS SEALED WITH  MAGIC WEBS."
6801 DISPLAY AT(3,1):"YOU LOSE ONE HIT POINT      FIGHTING THROUGH THEM."
6802 IT(8)=IT(8)-.5
6803 IF IT(8)=0 THEN A=0 :: RETURN
6804 IT(14)=1
6810 IF IT(4)=1 THEN 6900
6820 CALL KEY(1,K,S):: IF K<>18 THEN 6820
6830 RETURN
6900 DISPLAY AT(3,1):"BUT YOUR MAGIC SWORD CUTS   THEM AND YOU GET THE CRYSTAL"
6901 IT(8)=IT(8)+.5
6905 IT(14)=1
6910 GOTO 6820
7000 DISPLAY AT(1,8):"CATCH THE CRYSTAL"
7005 CALL SPRITE(#28,100,5,160,160)
7010 IF RND>.5 THEN @=128 ELSE @=120
7020 CALL SPRITE(#1,@,2,1,INT(RND*256)+1,20,0)
7030 CALL CM(C$,X,Y):: CALL MOTION(#28,0,X*3)
7040 CALL COINC(ALL,A)
7050 IF A THEN 7100
7060 CALL POSITION(#1,Y,X):: IF Y>200 THEN 7010 ELSE 7030
7100 IF @=120 THEN 7150
7110 CALL SOUND(100,-5,0):: IT(8)=IT(8)-.5 :: IF IT(8)=0 THEN A=0 :: RETURN
7120 GOTO 7060
7150 CALL MOTION(#1,0,0):: FOR A=0 TO 30 :: CALL SOUND(-10,1050,A):: NEXT A :: IT(15)=1 :: RETURN
7200 DISPLAY AT(1,1):"YOU MUST ANSWER MY RIDDLE"
7210 DISPLAY AT(3,3):"IT FLOWS AROUND":"   IN EVERYTHING"
7220 DISPLAY AT(5,3):"IT IS THE SINGLE MOST":"   VITAL THING"
7230 DISPLAY AT(7,1):"ON THE EARTH ITS EVERYWHERE"
7240 DISPLAY AT(8,1):"AND SO THE ANSWER MUST BE:"
7250 ACCEPT AT(9,1)SIZE(3)VALIDATE(UALPHA):X$
7260 IF X$="AIR" THEN 7300
7270 DISPLAY AT(12,1):"TRY AGAIN OR LEAVE"
7280 ACCEPT AT(12,20)SIZE(1)VALIDATE("TL"):X$
7290 IF X$="" THEN 7280
7291 IF X$="T" THEN CALL CLEAR :: GOTO 7200 ELSE RETURN
7300 DISPLAY AT(12,1):"THAT IS RIGHT. YOU MAY HAVE THE CRYSTAL."
7310 IT(16)=1
7320 CALL KEY(1,K,S):: IF K<>18 THEN 7320
7330 RETURN
7400 IF IT(10)THEN 7450
7410 DISPLAY AT(1,1):"YOU DO NOT HAVE THE SPECIAL FREE CRYSTAL."
7420 CALL KEY(1,K,S):: IF K<>18 THEN 7420
7430 RETURN
7450 DISPLAY AT(1,1):"YOU HAVE THE FREE CRYSTAL.  YOU MAY ENTER AND TAKE THIS ONE."
7460 IT(17)=1
7470 GOTO 7420
7600 FOR A=10 TO 17 :: IF IT(A)=0 THEN 7610
7605 NEXT A
7606 GOTO 7650
7610 DISPLAY AT(1,1):"YOU NEED ALL 8 CRYSTALS TO  ENTER HERE."
7620 GOTO 7420
7650 DISPLAY AT(9,1):"THIS IS THE DRAGON'S LAIR"
7660 X$="000"&STR$(IT(1))
7670 X$=SEG$(X$,LEN(X$)-2,3)
7680 FOR A=4 TO 8 :: X$=X$&SEG$(STR$(IT(A)),1,1):: NEXT A
7690 CALL CHAR(34,X$)
7700 RUN "DSK1.CQ3"
10000 SUB KEYJY(X,Y)
10010 CALL KEY(3,K,S)
10020 Y=4*(K=88)-4*(K=69)
10030 X=4*(K=83)-4*(K=68)
10040 SUBEND
10050 SUB ROOMDRAW(R)
10060 RANDOMIZE R
10070 CALL CLEAR
10080 FOR A=1 TO 5
10090 CALL HCHAR(INT(RND*24)+1,INT(RND*32)+1,38)
10100 NEXT A
10110 IF R<10 THEN CALL HCHAR(1,1,37,32)
10120 IF R>90 THEN CALL HCHAR(24,1,37,32)
10130 IF (R-1)/9=INT((R-1)/9)THEN CALL VCHAR(1,1,37,24)
10140 IF R/9=INT(R/9)THEN CALL VCHAR(1,32,37,24)
10145 RANDOMIZE
10150 SUBEND
10160 SUB SET(X())
10170 N=INT(RND*9)+1
10190 IF N=1 THEN S=36 :: C=7 :: X(3)=1 :: X(2)=1
10200 IF N=2 THEN S=36 :: C=2 :: X(3)=INT(RND*50)+1 :: X(2)=1
10210 IF N=3 THEN S=136 :: C=13 :: X(3)=1 :: X(2)=4
10220 IF N=4 THEN S=96 :: C=2 :: X(3)=1 :: X(2)=5
10230 IF N=5 THEN S=96 :: C=7 :: X(3)=2 :: X(2)=5
10240 IF N=6 THEN S=140 :: C=2 :: X(3)=1 :: X(2)=6
10250 IF N=7 THEN S=140 :: C=7 :: X(3)=2 :: X(2)=6
10252 IF N=8 THEN S=44 :: C=7 :: X(3)=1 :: X(2)=8
10254 IF N=9 THEN S=44 :: C=5 :: X(3)=1 :: X(2)=7
10260 CALL SPRITE(#1,S,C,INT(RND*100)+50,INT(RND*200)+20)
10270 SUBEND
10280 SUB CM(C$,X,Y)
10290 IF C$="J" THEN CALL JOYST(1,X,Y)ELSE CALL KEYJY(X,Y)
10300 SUBEND
